description = 'Distributed Streams'

def jdkHome = System.getenv("JAVA_HOME")

allprojects {
	group = 'org.dstream'

	repositories {
    	mavenCentral()
		mavenLocal();
	}
}

subprojects { subproject ->
	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'idea'

	sourceCompatibility = 1.8
	targetCompatibility = 1.8
	
	ext {
		slf4jlog4jversion = '1.7.10'
		log4jVersion = '1.2.17'
		commonsioVersion = '1.3.2'
		springVersion = '4.0.3.RELEASE'
		junitVersion = '4.8.2'	
		tezVersion = '0.7.0'
		guavaVersion = '14.0.1'
		javassistVersion = '3.20.0-GA'
	}

	sourceSets {
		test {
			resources {
				srcDirs = ['src/test/resources', 'src/test/java']
			}
		}
	}

	// dependencies that are common across all java projects
	dependencies {
		testCompile ("junit:junit:$junitVersion") {
			exclude group: 'org.hamcrest'
		}
		compile "log4j:log4j:$log4jVersion"
		compile "org.slf4j:slf4j-log4j12:$slf4jlog4jversion"
		compile "org.apache.commons:commons-io:$commonsioVersion"
	}

	task sourcesJar(type: Jar) {
		classifier = 'sources'
		from sourceSets.main.allJava
	}

	task javadocJar(type: Jar) {
		classifier = 'javadoc'
		from javadoc
	}

	artifacts {
		archives sourcesJar
		archives javadocJar
	}
}

project('dstream-core') {
	description = 'Core API for DStreams'
	dependencies {
		compile ("org.springframework:spring-aop:4.1.6.RELEASE") {
			exclude group: "org.springframework", module: "spring-beans"
		}
		compile "org.javassist:javassist:$javassistVersion"
		compile files('instrument/instrument.jar')
	}
}

project('dstream-tez') {
	description = 'Tez implementation of DStream API'
	dependencies {
		compile project(":dstream-core")
		compile ("org.apache.tez:tez-common:$tezVersion"){
			exclude group: 'com.google.guava'	
		}
		compile ("org.apache.tez:tez-dag:$tezVersion"){
			exclude group: 'com.google.guava'
		}
		compile ("org.apache.tez:tez-mapreduce:$tezVersion"){
			exclude group: 'com.google.guava'
		}
		compile "com.google.guava:guava:$guavaVersion"
	}
}

project('dstream-core') {
	description = 'Core API for DStreams'
	dependencies {
		compile ("org.springframework:spring-aop:4.0.3.RELEASE") {
			exclude group: "org.springframework", module: "spring-beans"
		}
	}
}

task wrapper(type: Wrapper) {
    description = 'Generates gradlew[.bat] scripts'
}
